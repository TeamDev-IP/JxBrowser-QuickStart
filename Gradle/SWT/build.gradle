/*
 *  Copyright 2021, TeamDev. All rights reserved.
 *
 *  Redistribution and use in source and/or binary forms, with or without
 *  modification, must retain the above copyright notice and the following
 *  disclaimer.
 *
 *  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 *  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 *  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 *  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 *  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 *  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 *  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 *  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 *  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 *  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 *  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building a CLI application
    id 'application'

    // This plugin automatically resolves SWT dependencies.
    id 'com.diffplug.gradle.eclipse.mavencentral' version '3.18.0' apply false
}

repositories {
    jcenter()
    // The repository for JxBrowser binaries.
    maven { url = 'https://europe-maven.pkg.dev/jxbrowser/releases' }
}

ext {
    jxBrowserVersion = '7.28.1'
}

dependencies {
    // Use JxBrowser cross-platform binaries
    implementation "com.teamdev.jxbrowser:jxbrowser-cross-platform:${jxBrowserVersion}"

    // Use JxBrowser SWT GUI toolkit
    implementation "com.teamdev.jxbrowser:jxbrowser-swt:${jxBrowserVersion}"
}

apply plugin: 'com.diffplug.gradle.eclipse.mavencentral'

eclipseMavenCentral {
    // Plugin documentation claims that they support versions 3.5.0 through 4.12.0.
    // Nevertheless all versions higher than 4.8.0 cannot be resolved.
    // Current version is bundled with the 3.107.0 version of the SWT.
    release '4.8.0', {
        implementation 'org.eclipse.swt'
        useNativesForRunningPlatform()
    }
}

application {
    // Define the main class for the application
    mainClassName = 'HelloWorld'
}

tasks.withType(JavaExec) {
    // Assign all Java system properties from
    // the command line to the JavaExec task.
    systemProperties System.properties
}
